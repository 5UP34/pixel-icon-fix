import Paths;
import PlayState;
import flixel.FlxG;
import flixel.util.FlxTimer;
import shaderslmfao.OverlayBlend;

import play.stage.Stage;

class LimoRideStage extends Stage {
	public function new() {
		super('limoRide');
	}

	var limoDancers:Array<Bopper> = [];

	override function buildStage() {
		super.buildStage();

		limoDancers = [];

		for (index in 0...5) {
			// Defined in scripts/props/limoDancer.hxc
			var dancer = new LimoDancer();
			dancer.x = 130 + (370 * index);
			dancer.y = -100; // TODO: Correct
			dancer.scrollFactor.set(0.4, 0.4);

			limoDancers.push(dancer);
			this.add(dancer);
		}
		trace('Added ' + limoDancers.length + ' limo dancers');

		// Apply sky shader.
		var skyOverlay:OverlayBlend = new OverlayBlend();
		var sunOverlay:FlxSprite = new FlxSprite().loadGraphic(Paths.image('limo/limoOverlay'));
		sunOverlay.setGraphicSize(Std.int(sunOverlay.width * 2));
		sunOverlay.updateHitbox();
		skyOverlay.funnyShit.input = sunOverlay.pixels;
		getNamedProp('limoSunset').shader = skyOverlay;

		// There's some commented-out shader BS in the original code.
		// I don't know what it's for, but it's not used in the game.
		// If you want to re-add it, go find it in version control.

		resetFastCar();
	}

	override function onBeatHit(curBeat:Int) {
		if (FlxG.random.bool(10) && fastCarCanDrive)
			fastCarDrive();
	}

	var fastCarCanDrive:Bool = false;

	function resetFastCar():Void {
		var fastCar = getNamedProp('fastCar');
		fastCar.y = FlxG.random.int(140, 250);
		fastCar.velocity.x = 0;
		fastCarCanDrive = true;
	}

	function fastCarDrive():Void {
		FlxG.sound.play(Paths.soundRandom('carPass', 0, 1), 0.7);

		getNamedProp('fastCar').velocity.x = (FlxG.random.int(170, 220) / FlxG.elapsed) * 3;
		fastCarCanDrive = false;
		new FlxTimer().start(2, function(tmr:FlxTimer)
		{
			resetFastCar();
		});
	}

	override function fetchAssetPaths():Array<String> {
		var results:Array<String> = super.fetchAssetPaths();

		// The limo dancer isn't built by the default stage function, so we need to add it here.
		results.push(Paths.image('limo/limoDancer'));
		// This graphic is applied by shader to the background, so it's not included in the default stage function.
		results.push(Paths.image('limo/limoOverlay'));
		results.push(Paths.sound('carPass0'));
		results.push(Paths.sound('carPass1'));

		return results;
	}

	override function kill():Void {
		// NOOOOO!!! Don't kill the limo dancers! They're doing their best!
		for (bopper in limoDancers) {
			bopper.kill();
		}
		limoDancers = [];
	}
}